actor "Rocket Launcher" : RocketLauncher
{
    Weapon.AmmoGive 4
    Weapon.SlotNumber 5
    Weapon.SlotPriority 0
    +INVENTORY.UNDROPPABLE
    States
    {
      Ready:
	/*TNT1 A 0 A_JumpIfInventory("Land Mine Layer",1,"Ready2")
	TNT1 A 0 A_GiveInventory("Land Mine Layer",1)
      Ready2:*/
	DMIS A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdReady")
        DMIS A 1 A_WeaponReady
        Loop

      StrongholdReady:
	MISG A 1 A_WeaponReady
	goto Ready

      Deselect:
	DMIS A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdDeselect")
        DMIS A 0 A_Lower
        DMIS A 1 A_Lower
        loop

      StrongholdDeselect:
        MISG A 0 A_Lower
        MISG A 1 A_Lower
        Goto Deselect

      Select:
		DMIS A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdSelect")
        DMIS A 0 A_Raise
        DMIS A 1 A_Raise
        Loop

      StrongholdSelect:
        MISG A 0 A_Raise
        MISG A 1 A_Raise
        Goto select

      Fire:
	DMIS A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdFire")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        DMIS A 2 
        DMIS A 4 bright A_GunFlash
        DMIS B 2 bright 
        goto FireMid
      
      VanillaFire:
        DRKL A 3 bright A_Light1
        DRKL B 4 bright
        DRKL C 1 bright A_Light2
        goto FireMid

      FireMid:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_FireCustomMissile("DoomRocket")
        goto FireEnd

      CoopMid:
        TNT1 A 0 A_FireCustomMissile("DoomRocket2")
        goto FireEnd
      
      FireEnd:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaEnd")
        DMIS D 3 bright
        DMIS C 3 bright
        DMIS BE 2
        DMIS FG 1
        DMIS A 0 A_Refire
        goto Ready
      
      RageEnd:
        DMIS DC 2 bright
        DMIS BFG 2
        DMIS A 0 A_Refire
        goto Ready
        
      VanillaEnd:
        DRKL C 3 bright
        DRKL D 4 bright
        DMIS B 5 A_Light0
        DMIS B 0 A_ReFire // 20 frames total
        Goto Ready

      StrongholdFire:
	MISF A 4 Bright A_GunFlash
        MISF B 5 Bright A_FireCustomMissile("DoomRocket2",0,1,0,0)
        MISF CD 5 Bright
	MISF D 0 A_ReFire
        Goto Ready
        
      Flash:
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons", 1, "StrongholdFlash")
        DMSF A 3 bright A_Light1
        DMSF B 2 bright
        DMSF C 2 bright A_Light2
        DMSF DE 3 bright
        Goto LightDone

      StrongholdFlash:
	TNT1 A 4 bright A_Light1
	TNT1 A 5 bright A_Light2
	TNT1 A 5 bright A_Light1
	TNT1 A 5 bright A_Light0
	stop

      Spawn:
        WDRL A -1
        Stop
    }
}

actor DoomRocket : Rocket
{
    Decal Scorch
    SeeSound "doomguy/rocklf"
    DeathSound "doomguy/rocklx"
    -FIREDAMAGE
    Damagetype "Explosive"
    Obituary "%k \cdmade\c- %o \cdride the rocket.\c-"
    States
    {
      Spawn:
        DMSL A 1 bright
        Loop

      Death:
        DMSL B 8 bright A_Explode
        DMSL C 6 bright
        DMSL D 4 bright
        Stop
    }
}

actor DoomRocket2 : DoomRocket { +THRUSPECIES Species "Player" }

ACTOR "Land Mine Layer" : Weapon
{
   Inventory.PickupMessage "You got some land mines!"
   Inventory.PickupSound "misc/w_pkup"
   Weapon.SlotPriority 1
   Weapon.AmmoGive 10
   Weapon.AmmoType "LandMineAmmo"
   Weapon.AmmoUse 1
   Weapon.AmmoType2 "LandMineAmmo"
   Weapon.AmmoUse2 1
   Tag "Land Mines"
   +INVENTORY.UNDROPPABLE
   States
   {
   Spawn:
      MINE A -1
      Loop
   Ready:
      LAYR A 1 A_WeaponReady
      Loop
   Deselect:
      LAYR A 0 A_Lower
      LAYR A 1 A_Lower
      Loop
   Select:
      LAYR A 0 A_Raise
      LAYR A 1 A_Raise
      Loop
   AltFire:
      LAYR B 8 A_PlaySound("weapons/minebeep")
      LAYD ABCDE 1
      TNT1 A 10
      LAYT A 1 A_PlaySound("weapons/minethrow")
      LAYT BCDEFGH 1
      LAYT I 1 A_FireCustomMissile("Landmine2",0,1,0,8)
      LAYT JKLMNOPQR 1
      TNT1 A 10
      LAYU ABCDE 1
      LAYR A 6
      Goto Ready
   Fire:
      LAYR B 8 A_PlaySound("weapons/minebeep")
      LAYD ABCDE 1
      TNT1 A 5
      TNT1 A 9 A_FireCustomMissile("Landmine3",0,1,0,0)
      LAYU ABCDE 1
      LAYR A 4
      Goto Ready
   }
}

ACTOR Landmine
{
   Radius 8 //was 5
   Height 5
   Mass 1000000
   Health 20
   Damage 10
   SeeSound "weapons/tink"
   ExplosionDamage 384
   ExplosionRadius 160
   DONTHURTSHOOTER
   Speed 0
   +NOBLOOD
   -SHOOTABLE
   +MISSILE
   +THRUSPECIES
   Species "Player"
   States
   {
   Spawn:
      MINE A 0 A_Gravity
      MINE AB 5
      Loop
   Death:
      MISL B 0 A_PlaySound("weapons/mineexpl")
      MISL B 8 A_Explode
      MISL C 6
      MISL D 4
      Stop
   }
}

ACTOR Landmine2
{
   Radius 8 //was 5
   Height 5
   Mass 1000000
   Health 20
   Damage 6
   SeeSound "weapons/tink"
   DONTHURTSHOOTER
   Speed 15
   +NOBLOOD
   -SHOOTABLE
   +MISSILE
   +DOOMBOUNCE
   +THRUSPECIES
   Species "Player"
   States
   {
   Spawn:
      MINE A 0 A_Gravity
      MINE AB 5
      Loop
   Death:
      MINE A 0 A_SpawnItem("Landmine")
      Stop
   }
}

ACTOR Landmine3 : Landmine2
{
   Speed 2
   Damage 7
}

actor LandMineAmmo: Ammo
{
    Inventory.Amount 1
    Inventory.MaxAmount 20

    Ammo.BackpackAmount 2
    Ammo.BackpackMaxAmount 40

    Inventory.Icon "AMSML0"
}
