actor " Pistol " : Weapon// : Pistol
{
    Weapon.SelectionOrder 1900
    Decal BulletChip
    Weapon.SlotNumber 1
    Weapon.SlotPriority 1
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    +WEAPON.WIMPY_WEAPON
    +INVENTORY.UNDROPPABLE
    DamageType Pistol
    Inventory.Pickupmessage "i eat you"
    Obituary "$SAMSARA_DOOMGUY_OB_PISTOL"
    States
    {
      Ready:
        DPIS A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdReady")
        DPIS A 1 A_WeaponReady
        Loop

      Deselect:
        DPIS A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdDeselect")
        DPIS A 0 A_Lower
        DPIS A 1 A_Lower
        Loop

      Select:
        DPIS A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdSelect")
        DPIS A 0 A_Raise
        DPIS A 1 A_Raise
        Loop

      StrongholdDeselect:
        PISG A 0 A_Lower
        PISG A 1 A_Lower
        goto Deselect

      StrongholdSelect:
        PISG A 0 A_Raise
        PISG A 1 A_Raise
        goto Select

      Fire:
         TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmo")

         Fire2:
	DPIS A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdStart")
        DPIS A 4
	goto Fire3

	StrongholdStart:
	PISG A 4
	Goto Fire3

	Fire3:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
                TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireBullets (5.6, 0, 1, 5, "DoomBulletPuff2")
        goto FireAnim
                
         CheckForAmmo:
         TNT1 A 0 A_JumpIfInventory("Clip",1,"Fire2")
                TNT1 A 0 A_SelectWeapon(" Fist ")
                goto Deselect

      CoopFire:
         TNT1 A 0
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireCustomMissile("DoomBullet2", 0, 1, 0, 1)
        Goto FireAnim

      FireAnim:
         TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"TakePistolAmmo")
         FireAnim2:
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdAnim")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
            TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageAnim")
        DPIS B 2 bright
        DPIS C 1 bright
        DPIS CEEDDBB 1
        DPIS A 5 A_ReFire
        Goto Ready

      RageAnim:
        DPIS BC 1 bright
        DPIS EDB 1
        DPIS A 3 A_Refire
        goto Ready
        
      VanillaFire:
        DPIS F 6 BRIGHT A_GunFlash
        DPIS G 4
        DPIS H 5 A_ReFire
        Goto Ready
                
         TakePistolAmmo:
         TNT1 A 0 A_TakeInventory("Clip",1)
                goto FireAnim2

      StrongholdAnim:
        PISF A 6 A_GunFlash
        PISG C 4
        PISG B 5 A_ReFire
        goto Ready

      Hold:
         TNT1 A 0 A_JumpIfInventory("PistolModeOn",1,"CheckForAmmoHold")
         Hold2:
	TNT1 A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdHold")
        DPIS A 4
	Goto Hold3
	
	StrongholdHold:
	PISG A 4
	Goto Hold3

	Hold3:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopHold")
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireBullets (5.6, 0, -1, 5, "DoomBulletPuff2")
        Goto FireAnim
                
         CheckForAmmoHold:
         TNT1 A 0 A_JumpIfInventory("Clip",1,"Hold2")
                TNT1 A 0 A_SelectWeapon(" Fist ")
                goto Deselect
        
      CoopHold:
         TNT1 A 0
        TNT1 A 0 A_PlayWeaponSound("doomguy/pistol")
        TNT1 A 0 A_FireCustomMissile("DoomBullet2", random(-560,560)/100.00, 1, 0, 1)
        Goto FireAnim
        
      Flash:
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFlash")
        DPSF A 3 Bright A_Light1
        TNT1 A 2 A_Light1
        Goto LightDone
        
         VanillaFlash:
        TNT1 A 3 Bright A_Light1
        TNT1 A 2 A_Light1
        Goto LightDone

      Spawn:
        WPST A -1
        Stop
    }
}

actor DoomBulletPuff2 : DoomBulletPuff { DamageType Pistol }

actor DoomBullet2: FastProjectile
{
    Damage (Random(1,4)*5)
    Radius 1
    Height 1
    Speed 320
    Projectile
    DamageType "Pistol"
    Renderstyle "Normal"
    Species "Player"
    +BLOODSPLATTER
    +THRUSPECIES
    +NOTIMEFREEZE
    Alpha 1.0
    Decal Bulletchip
    States
    {
      Spawn:
        TNT1 A 1
        loop

      Death:
        TNT1 A 1 A_SpawnItem("DoomBulletPuff2")
        stop

      XDeath:
        TNT1 A 1
        stop
    }
}