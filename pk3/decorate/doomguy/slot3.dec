actor "Super Shotgun" : SuperShotgun
{
    Weapon.SlotPriority 0
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 20
    Decal BulletChip
    Weapon.SlotNumber 3
    +THRUGHOST
    +INVENTORY.UNDROPPABLE
    AttackSound ""
    Obituary "%k \cdsmeared\c- %o \cdacross the wall with the SSG.\c-"
    States
    {
      Ready:
	/*TNT1 A 0 A_JumpIfInventory("Automatic Shotgun",1,"Ready2")
	TNT1 A 0 A_GiveInventory("Automatic Shotgun",1)
      Ready2:*/
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdReady")
        DSSG A 1 A_WeaponReady
        Loop

      StrongholdReady:
        SHT2 A 1 A_WeaponReady
        Goto Ready

      Deselect:
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdDeselect")
        DSSG A 0 A_Lower
        DSSG A 1 A_Lower
        Loop

      Select:
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdSelect")
        DSSG A 0 A_Raise
        DSSG A 1 A_Raise
        Loop

      StrongholdDeselect:
        SHT2 A 0 A_Lower
        SHT2 A 1 A_Lower
        Goto Deselect

      StrongholdSelect:
        SHT2 A 0 A_Raise
        SHT2 A 1 A_Raise
        Goto Select

      Fire:
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdStart")
        DSSG A 3
        TNT1 A 0 A_PlayWeaponSound ("doomguy/sshotf")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_FireBullets (11.2, 7.1, 20, 5, "DoomBulletPuff")
        goto FireEnd

      StrongholdStart:
	SHT2 A 3
	TNT1 A 0 A_PlayWeaponSound ("weapons/sshotf")
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopMid")
        TNT1 A 0 A_FireBullets (11.2, 7.1, 20, 5, "DoomBulletPuff")
	goto FireEnd

      CoopMid:
        TNT1 AAAAA 0 A_FireCustomMissile("DoomBullet", random(-1125, 1125)/100.00, 0, 0, 1, 0, random(-7125, 7125)/1000.00)
        TNT1 AAAAA 0 A_FireCustomMissile("DoomBullet", random(-800, 800)/100.00, 0, 0, 1, 0, random(-5700, 5700)/1000.00)
        TNT1 AAAA  0 A_FireCustomMissile("DoomBullet", random(-675, 675)/100.00, 0, 0, 1, 0, random(-4275, 4275)/1000.00)
        TNT1 AAA   0 A_FireCustomMissile("DoomBullet", random(-450, 450)/100.00, 0, 0, 1, 0, random(-2850, 2850)/1000.00)
        TNT1 AA    0 A_FireCustomMissile("DoomBullet", random(-225, 225)/100.00, 0, 0, 1, 0, random(-1425, 1425)/1000.00)
        TNT1 A     0 A_FireCustomMissile("DoomBullet", 0, 1, 0, 1, 0, 0)
        goto FireEnd

      FireEnd:
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdEnd")
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("RuneDoubleFiringSpeed", 1, "RageEnd")
        DSSG A 2 bright A_ZoomFactor(0.985, ZOOM_INSTANT)
        DSSG A 4 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSSG A 3
        DSSG B 2
        DSSG C 3
        DSSG DE 4 A_CheckReload
        DSSG F 2 A_PlayWeaponSound("doomguy/sshoto")
        DSSG GHIJ 2
        DSSG K 3
        DSSG L 3 A_PlayWeaponSound("doomguy/sshotl")
        DSSG MNOP 2
        DSSG Q 1
        DSSG Q 1 A_PlayWeaponSound("doomguy/sshotc")
        DSSG RST 3 A_ReFire
        DSSG A 2 A_Refire
        goto Ready

      RageEnd:
        DSSG A 2 bright
        DSSG A 4 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSSG A 4
        DSSG BCC 2
        DSSG DE 4 A_CheckReload
        DSSG F 2 A_PlayWeaponSound("doomguy/sshoto")
        DSSG GHIJJK 2
        DSSG L 4 A_PlayWeaponSound("doomguy/sshotl")
        DSSG MNOOP 2
        DSSG Q 2 A_PlayWeaponSound("doomguy/sshotc")
        DSSG RSSTTA 2 A_Refire
        goto Ready

      VanillaFire:
        DSSG A 7 bright
        DSG2 B 7
        DSG2 C 7 A_CheckReload
        DSG2 D 7 A_PlayWeaponSound("doomguy/sshoto")
        DSG2 E 7
        DSG2 F 7 A_PlayWeaponSound("doomguy/sshotl")
        DSG2 G 6
        TNT1 A 0 A_PlayWeaponSound("doomguy/sshotc")
        DSG2 H 6 A_Refire
        DSSG A 5 A_ReFire
        Goto Ready

  StrongholdEnd:
    SHT2 M 2 Bright
    SHT2 N 3 Bright
    SHT2 O 2 Bright
    SHT2 B 3
    SHT2 C 4
    SHT2 D 4 A_CheckReload
    SHT2 C 3
    SHT2 E 3 A_PlayWeaponSound ("weapons/sshoto")
    SHT2 F 3
    SHT2 G 4
    SHT2 H 4
    SHT2 I 3 A_PlayWeaponSound ("weapons/sshotl")
    SHT2 J 3
    SHT2 K 3
    SHT2 L 4
    SHT2 E 0 A_PlayWeaponSound ("weapons/sshotc")
    SHT2 E 6 A_ReFire
    SHT2 A 5 A_ReFire
    goto Ready

      Flash:
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons", 1, "StrongholdFlash")
        DSSF A 3 bright A_Light1
        DSSF B 3 bright A_Light2
        Goto LightDone

      StrongholdFlash:
    TNT1 A 2 bright A_Light1
    TNT1 A 3 bright A_Light2
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop

      Spawn:
        WSSG A -1
        Stop
    }
}

Actor "Automatic Shotgun" : Weapon
{
   +AMMO_OPTIONAL
   Inventory.PickupMessage "You got the Auto Shotgun!"
   Inventory.PickupSound "misc/w_pkup"
   Weapon.SlotPriority 1
   Weapon.AmmoType "AutoShotgunClip"
   Weapon.AmmoGive1 20
   Weapon.AmmoUse 1
   Weapon.AmmoType2 "Shell"
   Weapon.AmmoGive2 0
   Weapon.AmmoUse2 0
   Tag "Auto Shotgun"
   +INVENTORY.UNDROPPABLE
  states
  {
  Spawn:
    WASG A -1
    stop
  Ready:
    ASGG A 1 A_WeaponReady
    loop
  Deselect:
    ASGG A 0 A_Lower
    ASGG A 1 A_Lower
    loop
  Select:
    ASGG A 0 A_Raise
    ASGG A 1 A_Raise
    loop
  Fire:
  Hold:
    ASGG A 0 A_JumpIfInventory("AutoShotgunClip", 1, "Shoot")
    ASGG A 0 A_JumpIfInventory("Shell", 1, "Altfire")
    Goto Stall
  Stall:
    ASGG A 2 A_PlayWeaponSound("Weapons/asgout")
    Goto Ready
  Shoot:
    ASGG A 1
    ASGG L 0 A_GunFlash
    ASGG A 0 A_PlaySoundEX("Weapons/asgfir","auto",0)
    TNT1 AA 0 A_FireCustomMissile("DoomBullet", random(-500, 500)/100.00, 0, 0, 1, 0, random(-400, 400)/100.00)
    TNT1 AA 0 A_FireCustomMissile("DoomBullet", random(-400, 400)/100.00, 0, 0, 1, 0, random(-320, 320)/100.00)
    TNT1 A  0 A_FireCustomMissile("DoomBullet", random(-300, 300)/100.00, 0, 0, 1, 0, random(-240, 240)/100.00)
    TNT1 A  0 A_FireCustomMissile("DoomBullet", random(-200, 200)/100.00, 0, 0, 1, 0, random(-160, 160)/100.00)
    TNT1 A  0 A_FireCustomMissile("DoomBullet", random(-100, 100)/100.00, 1, 0, 1, 0, random(-80, 80)/100.00)
    ASGG L 2
    ASGG M 3
    ASGG N 2
    ASGG A 4 A_PlaySoundEX("weapons/asgld1","auto",0)
    ASGG A 3
    ASGG A 0 A_ReFire
    goto Ready
  Altfire:
    ASGG BCDEF 3
    ASGG G 3 A_PlayWeaponSound("Weapons/asgout")
    ASGG HIJ 3
    ASGG k 15
    ASGG JIH 3
  Reloading1:
    ASGG G 0 A_TakeInventory("Shell",1)
    ASGG G 0 A_GiveInventory("AutoShotgunClip",1)
    ASGG G 0 A_JumpIfInventory("AutoShotgunClip",20,"Reloading2")
    ASGG G 0 A_JumpIfInventory("Shell",1,"Reloading1")
    ASGG G 0
    Goto Reloading2
  Reloading2:
    ASGG G 3 A_PlayWeaponSound("Weapons/asgin")
    ASGG FEDCB 3
    ASGG A 8 A_PlaySoundEX("weapons/asgld1","auto",0)
    goto Ready
  Flash:
    TNT1 A 4 bright A_Light1
    TNT1 A 3 bright A_Light2
    TNT1 A 0 bright A_Light0
    stop
    }
}

ACTOR AutoShotgunClip: Ammo
{
     Inventory.Icon "AMSMB0"
     Inventory.MaxAmount 20
     Ammo.BackpackAmount 0
     Ammo.BackpackMaxAmount 20
     +Inventory.IgnoreSkill
}
