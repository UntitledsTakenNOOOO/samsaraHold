actor " Shotgun " : Shotgun
{
    Weapon.AmmoType "Shell"
    Decal BulletChip
    Weapon.SlotNumber 2
    +THRUGHOST
    +INVENTORY.UNDROPPABLE
    AttackSound ""
    Obituary "%k \cdshowed\c- %o \cdhis boomstick.\c-"
    States
    {
      Ready:
        DSHT A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdReady")
        DSHT A 1 A_WeaponReady
        Loop

      StrongholdReady:
        SHTG A 1 A_WeaponReady
        Goto Ready

      Deselect:
	DSHT A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdDeselect")
        DSHT A 0 A_Lower
        DSHT A 1 A_Lower
        Loop

      Select:
        DSHT A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdSelect")
        DSHT A 0 A_Raise
        DSHT A 1 A_Raise
        Loop

      StrongholdDeselect:
        SHTG A 0 A_Lower
        SHTG A 1 A_Lower
        Goto Deselect

      StrongholdSelect:
        SHTG A 0 A_Raise
        SHTG A 1 A_Raise
        Goto Select

      Fire:
        DSHT A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdStart")
        DSHT A 3
        DSHT A 0 A_PlayWeaponSound("doomguy/shotgf")
        TNT1 A 0 A_GunFlash
	TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        TNT1 A 0 A_FireBullets (5.6, 0, 8, 5, "DoomBulletPuff")
        goto FireAnim

      StrongholdStart:
        SHTG A 3
        SHTG A 0 A_PlayWeaponSound("weapons/shotgf")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn", 1, "StrongholdCoopFire")
        TNT1 A 0 A_FireBullets (4, 3, 8, 5, "DoomBulletPuff")
        goto FireAnim

      CoopFire:
        TNT1 AA 0 A_FireCustomMissile("DoomBullet", frandom(-5.6, 5.6), 0, 0, 1, 0)
        TNT1 AA 0 A_FireCustomMissile("DoomBullet", frandom(-4.2, 4.2), 0, 0, 1, 0)
        TNT1 AA 0 A_FireCustomMissile("DoomBullet", frandom(-2.8, 2.8), 0, 0, 1, 0)
        TNT1 A  0 A_FireCustomMissile("DoomBullet", frandom(-1.4, 1.4), 1, 0, 1, 0)
        goto FireAnim

      StrongholdCoopFire:
        TNT1 AA 0 A_FireCustomMissile("DoomBullet", frandom(-4.0, 4.0), 0, 0, 1, 0, frandom(-0.3, 0.3))
        TNT1 AA 0 A_FireCustomMissile("DoomBullet", frandom(-3.0, 3.0), 0, 0, 1, 0, frandom(-0.225, 0.225))
        TNT1 AA 0 A_FireCustomMissile("DoomBullet", frandom(-2.0, 2.0), 0, 0, 1, 0, frandom(-0.15, 0.15))
        TNT1 A  0 A_FireCustomMissile("DoomBullet", frandom(-1.0, 1.0), 1, 0, 1, 0, frandom(-0.75, 0.75))
        goto FireAnim
        
      FireAnim:
        DSHT A 0 A_JumpIfInventory("StrongholdWeapons",1,"StrongholdAnim")
        DSHT A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 1, "RageAnim")
        TNT1 A 0 A_ZoomFactor(0.995, ZOOM_INSTANT)
        DSHT A 2 bright
        DSHT A 5 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSHT BCDEE 1
        DSHT F 3
        DSHT G 3
        DSHT H 4
        DSHT G 4
        DSHT F 3
        DSHT ED 2
        DSHT C 1
        DSHT B 3 A_Refire
        DSHT A 4 A_ReFire
        Goto Ready

      RageAnim:
        DSHT A 2 bright
        DSHT A 6 bright A_ZoomFactor(1, ZOOM_INSTANT)
        DSHT CE 2
        DSHT FG 4
        DSHT H 6
        DSHT GF 4
        DSHT EDC 2
        DSHT B 2 A_Refire
        DSHT A 4 A_Refire
        goto Ready

      VanillaFire:
        DSHT A 7 bright
        DSHT IJ 5
        DSHT K 4
        DSHT JI 5
        DSHT A 3
        DSHT A 7 A_ReFire
        Goto Ready

      StrongholdAnim:
        SHTG E 2
        SHTG F 3
        SHTG G 2
        SHTG BC 5
        SHTG D 4
        SHTG CB 5
        SHTG A 3
        SHTG A 7 A_ReFire
	Goto Ready

      Flash:
        TNT1 A 0 A_JumpIfInventory("StrongholdWeapons", 1, "StrongholdFlash")
        DSHF A 3 bright A_Light1
        DSHF B 4 bright A_Light2
        Goto LightDone

      StrongholdFlash:
	TNT1 A 4 bright A_Light1
	TNT1 A 3 bright A_Light2
	TNT1 A 0 bright A_Light0
	stop

      Spawn:
        WSHT A -1
        Stop
    }
}
