actor "Zorch Propulsor" : Weapon
{
    Weapon.SelectionOrder 2500
    Weapon.SlotNumber 5
    Inventory.PickUpSound "chex/weaponget"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 2
    Weapon.AmmoType "RocketAmmo"
    +WEAPON.NOAUTOFIRE
    //+AMMO_OPTIONAL
    +INVENTORY.UNDROPPABLE
    Inventory.PickupMessage "You got the Zorch Propulsor!"
    States
    {
      Ready:
        MISC A 1 A_WeaponReady
        Loop

      Deselect:
        MISC A 1 A_Lower
        Loop

      Select:
        MISC A 1 A_Raise
        Loop

      Fire:
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        MISC B 8 A_GunFlash
	    TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"RJFire")
        MISC B 12 A_FireCustomMissile("ChexRocket")
        MISC A 4
        MISC B 0 A_ReFire
        Goto Ready
		
	  RJFire:
        MISC B 12 A_FireCustomMissile("ChexRocketRJ")
        MISC A 4
        MISC B 0 A_ReFire
        Goto Ready

      CoopFire:
        MISC B 8 A_GunFlash
	    TNT1 A 0 A_JumpIfInventory("RJingModeOn",1,"RJFireCoop")
        MISC B 12 A_FireCustomMissile("ChexRocket2")
        MISC A 4
        MISC B 0 A_ReFire
        Goto Ready
		
	  RJFireCoop:
        MISC B 12 A_FireCustomMissile("ChexRocket2RJ")
        MISC A 4
        MISC B 0 A_ReFire
        Goto Ready

      Flash:
        MCSF A 3 Bright A_Light1
        MCSF B 4 Bright
        MCSF CD 4 Bright A_Light2
        Goto LightDone

      Spawn:
        LAUC A -1
        Stop
    }
}

actor ChexRocket
{
    Radius 11
    Height 8
    Speed 20
    Damage 20
    Projectile
    +RANDOMIZE
    +BLOODLESSIMPACT
    +BRIGHT
    SeeSound "chex/rocklf"
    DeathSound "chex/rocklx"
    Obituary "%k \chzorched\c- %o \chwith way more force than needed.\c-"
    DamageType "Zorch"
    States
    {
      Spawn:
        CMIS A 1 Bright
        Loop

      Death:
        CMIS B 8 Bright A_Explode(128,128,0)
        CMIS C 6 Bright
        CMIS D 4 Bright
        Stop
    }
}

actor ChexRocketRJ : ChexRocket
{
    States
	{
      Death:
        CMIS B 8 Bright A_Explode(128,128)
        CMIS C 6 Bright
        CMIS D 4 Bright
        Stop
	}
}

actor ChexRocket2 : ChexRocket { +THRUSPECIES Species "Player" }
actor ChexRocket2RJ : ChexRocketRJ { +THRUSPECIES Species "Player" }
