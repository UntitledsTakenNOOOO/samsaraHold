actor "Double Shotgun": InstantSwitchWeapon
{
    //Weapon.BobRangeX 0
    //Weapon.BobRangeY 0.5
    //Weapon.BobSpeed 1.1
    //Weapon.BobStyle Alpha
    Weapon.Kickback 100
    Weapon.SelectionOrder 6
    Weapon.AmmoUse 1
    Weapon.AmmoGive 10
    Weapon.AmmoType "Shell"

    +UNDROPPABLE
    +THRUGHOST
    +NOALERT
    
    Weapon.SisterWeapon "Spectral DSG"
    Inventory.Icon "QDBSZ0"
    
    Inventory.PickupSound "quakeweps/weaponpickup"
    Inventory.PickupMessage "You got the double-barreled shotgun"
    Obituary "%k \csgave\c- %o \cstwo loads of buckshot.\c-"
    
    //Decal BulletChip
    
    States
    {
      Ready:
        QDBS A 1 A_WeaponReady
        Loop
        
      Fire:
        QDBS A 0 ACS_ExecuteAlways(312, 0, 4, 3, 17)  
        QDBS A 0 A_PlaySound("quakeweps/ssgfire", CHAN_WEAPON)
        TNT1 A 0 A_JumpIfInventory("QuakeInvisTimer", 1, 2)
        TNT1 A 0 A_AlertMonsters

        QDBS A 0 A_JumpIfInventory("CoopModeOn", 1, "CoopFire")
        QDBS A 0 A_FireBullets(7, 5, 14, 4, "QuakeBulletPuff", FBF_USEAMMO | FBF_NORANDOM)
        goto Fire2
        
      CoopFire:
        TNT1 A 0 A_JumpIfInventory("QuakeModeOn",1,"VanillaFire")
        QDBS AAAAA 0 A_FireCustomMissile("QuakeBullet", frandom(-7.00,7.00), 0, 0, 0, 0, frandom(-5.00,5.00))
        QDBS AAAAA 0 A_FireCustomMissile("QuakeBullet", frandom(-6.50,6.50), 0, 0, 0, 0, frandom(-4.50,4.50))
        QDBS AAA 0 A_FireCustomMissile("QuakeBullet", frandom(-6.00,6.00), 0, 0, 0, 0, frandom(-4.00,4.00))
        QDBS A 0 A_FireCustomMissile("QuakeBullet", frandom(-4.00,4.00), 1, 0, 0, 0, frandom(-2.00,2.00))
        goto Fire2
        
      VanillaFire:
        QDBS AAAAA 0 A_FireCustomMissile("QuakeBullet", frandom(-7.00,7.00), 0, 0, 0, 0, frandom(-5.00,5.00))
        QDBS AAAAA 0 A_FireCustomMissile("QuakeBullet", frandom(-6.50,6.50), 0, 0, 0, 0, frandom(-4.50,4.50))
        QDBS AAA 0 A_FireCustomMissile("QuakeBullet", frandom(-6.00,6.00), 0, 0, 0, 0, frandom(-4.00,4.00))
        QDBS AA 0 A_FireCustomMissile("QuakeBullet", frandom(-4.00,4.00), 0, 0, 0, 0, frandom(-2.00,2.00))
        QDBS A 0 A_FireCustomMissile("QuakeBullet", frandom(-4.00,4.00), 1, 0, 0, 0, frandom(-2.00,2.00))
        goto Fire2

        // 25 tics
      Fire2:
        QDBS B 1 bright
        QDBS C 1 bright
        QDBS D 2 bright
        QDBS E 2
        QDBS E 5
        QDBS F 4
        QDBS G 3
        QDBS H 3
        QDBS I 2
        QDBS A 2
        QDBS I 0 A_ReFire
        Goto Ready
        
      Spawn:
        QDBS Z -1
        Stop
    }
}

actor "Spectral DSG": "Double Shotgun"
{
    Weapon.AmmoType ""
    Weapon.AmmoUse  0
    Weapon.AmmoGive 0

    Weapon.SisterWeapon "Double Shotgun"
    +POWERED_UP
    
    States
    {
      Fire:
        TNT1 A 0 A_JumpIfInventory("QuakeInvisTimer", 1, 2)
        TNT1 A 0 A_AlertMonsters

        QDBS A 0 ACS_ExecuteAlways(312, 0, 4, 3, 17)  
        QDBS A 0 ACS_ExecuteAlways(317, 0, 8)

        QDBS A 0 A_PlaySound("quakeweps/ssgfire", CHAN_WEAPON)
        QDBS A 0 A_PlaySound("weapons/sigil", 6)
        QDBS A 0 A_FireBullets(7, 5, 14, 4, "QuakeSpectralPuff", FBF_NORANDOM)
        goto Fire2
    }
}
