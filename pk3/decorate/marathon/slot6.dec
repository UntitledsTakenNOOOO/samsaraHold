actor UsingToasty: Boolean {}
Actor ToztCounter: Counter { Inventory.MaxAmount 7 }

actor "TOZT-7 Napalm Unit" : Weapon// replaces BFG9000
{
    //Weapon.BobRangeX 0
    //Weapon.BobStyle Smooth
    Weapon.PreferredSkin "SOFlamerSkin"
    Weapon.SelectionOrder 700
    Weapon.SlotNumber 6
    Inventory.PickupMessage "You picked up a TOZT-7 Backpack Napalm Unit!"
    Weapon.AmmoType		"NapalmInTank"
    Weapon.AmmoGive		500
    Weapon.AmmoUse		1
    Scale 0.35
    +AMMO_OPTIONAL
    +NOALERT
    +INVENTORY.UNDROPPABLE
    Radius				12
    Height				12
    States
    {
      Spawn:
        THRW A -1
        STOP

      Ready:
        FLMT A 1 A_WeaponReady
        TNT1 A 0 A_JumpIfNoAmmo("Reload")
        LOOP

      Deselect:
        FLMT A 1 A_Lower
        LOOP

      Select: // 30 tics in Marathon to ready
        TNT1 A 0 A_TakeInventory("UsingSMG",1)
        TNT1 A 0 A_TakeInventory("UsingPistols",1)
        TNT1 A 0 A_TakeInventory("UsingFists",1)
        TNT1 A 0 A_TakeInventory("UsingShotguns",1)
        TNT1 A 0 A_TakeInventory("UsingFusionPistol",1)
        TNT1 A 0 A_TakeInventory("UsingAssaultRifle",1)
        TNT1 A 0 A_TakeInventory("UsingSpanker",1)
        //TNT1 A 0 A_TakeInventory("UsingToasty",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon",1)
        TNT1 A 0 A_TakeInventory("UsingAlienWeapon2",1)
        TNT1 A 0 A_TakeInventory("UsingWMC",1)
        TNT1 A 0 A_TakeInventory("UsingDualPistols",1)
        TNT1 A 0 A_TakeInventory("UsingDualShotguns",1)
        TNT1 A 0 A_GiveInventory("UsingToasty",1)
        TNT1 A 0 A_JumpIfNoAmmo("Reload2")
        goto Select2

	  Select2:
        FLMT A 1 A_Raise
        LOOP

      Fire:
        TNT1 A 0 A_JumpIf(waterlevel == 3, "NothingFire")
        TNT1 A 0 A_JumpIfNoAmmo("Reload")
        TNT1 A 0 A_JumpIfInventory("CoopModeOn",1,"CoopFire")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("FlameShot",0,1,0,3)
        TNT1 A 0 A_PlaySound("marathon/flamethrower/fire")
        TNT1 A 0 A_Recoil(0.1)
        FLMT B 1 Bright
        TNT1 A 0 A_Refire
        Goto Ready

      CoopFire:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile("FlameShot2",0,1,0,3)
        TNT1 A 0 A_PlaySound("marathon/flamethrower/fire")
        TNT1 A 0 A_Recoil(0.1)
        FLMT B 1 Bright
        TNT1 A 0 A_Refire
        Goto Ready

      NothingFire:
        TNT1 A 0
        goto Ready

      Reload:
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
        Goto Ready//RIdle
        FLMT A 0 Offset(0,32)
        FLMT A 1 Offset(0,35)
        FLMT A 1 Offset(0,40)
        FLMT A 1 Offset(0,45)
        FLMT A 1 Offset(0,50)
        FLMT A 1 Offset(0,55)
        FLMT A 1 Offset(0,60)
        FLMT A 1 Offset(0,65)
        FLMT A 1 Offset(0,70)
        FLMT A 1 Offset(0,75)
        FLMT A 1 Offset(0,80) // 10
        FLMT A 1 Offset(0,85)
        FLMT A 1 Offset(0,90)
        FLMT A 1 Offset(0,95)
        FLMT A 1 Offset(0,100)
        FLMT A 1 Offset(0,105)
        FLMT A 1 Offset(0,110)
        FLMT A 1 Offset(0,115)
        FLMT A 1 Offset(0,120)
        FLMT A 1 Offset(0,125)
        FLMT A 1 Offset(0,130) // 20
        FLMT A 1 Offset(0,135)
        FLMT A 1 Offset(0,140)
        FLMT A 26

      Reload2:
        TNT1 A 0 A_JumpIfInventory("NapalmInTank",0,"Select2")
        TNT1 A 0 A_JumpIfInventory("Cell",1,1)
		goto Select2
        TNT1 A 0 A_TakeInventory("Cell",1)
        TNT1 A 0 A_GiveInventory("NapalmInTank",1)
        loop
    }
}

actor NapalmInTank : Ammo
{
    +IGNORESKILL
    Inventory.MaxAmount 210
}

actor FlameShot
{
    Radius 6
    Height 8
    Speed 50
    Damage (random(10,14))
    DamageType Fire
    Scale 0.85
    DeathSound ""
    Projectile
    Obituary "%k \cnmade\c- %o \cnall TOZT.\c-"
    +RANDOMIZE
    +RIPPER
    +NOBOSSRIP
    +BLOODLESSIMPACT
    States
    {
      Spawn:
        FLMZ A 1 BRIGHT
		FLMZ B 1 BRIGHT A_GiveInventory("ToztCounter",1)
		FLMZ C 1 BRIGHT A_GiveInventory("ToztCounter",1)
		FLMZ D 1 BRIGHT A_GiveInventory("ToztCounter",1)
		FLMZ E 1 BRIGHT A_GiveInventory("ToztCounter",1)
		FLMZ F 1 BRIGHT A_GiveInventory("ToztCounter",1)
		FLMZ G 1 BRIGHT A_GiveInventory("ToztCounter",1)
        Stop

      Death:
		TNT1 A 0 A_JumpIfInventory("ToztCounter",6,"Death6")
		TNT1 A 0 A_JumpIfInventory("ToztCounter",5,"Death5")
		TNT1 A 0 A_JumpIfInventory("ToztCounter",4,"Death4")
		TNT1 A 0 A_JumpIfInventory("ToztCounter",3,"Death3")
		TNT1 A 0 A_JumpIfInventory("ToztCounter",2,"Death2")
		TNT1 A 0 A_JumpIfInventory("ToztCounter",1,"Death1")
        FLMZ A 1 Bright
	  Death1:
        FLMZ B 1 Bright
	  Death2:
        FLMZ C 1 Bright
	  Death3:
        FLMZ D 1 Bright
	  Death4:
        FLMZ E 1 Bright
	  Death5:
        FLMZ F 1 Bright
	  Death6:
        FLMZ G 1 Bright
        Stop
    }
}

actor FlameShot2 : Flameshot { Species "Player" +THRUSPECIES }
