ACTOR EnhancedCaco 3118
{
//$Category Stronghold_Monsters
   painchance "Stunner", 128
   painchance "MightyBoot", 255
   DamageFactor "CorvusCrossbow", 1.1
   DamageFactor "DragonClaw", 1.5
   DamageFactor "HexenIce", 1.4
   DamageFactor "DukeIce", 1.4
   spawnid 245
   Health 300
   Speed 8
   Radius 31
   Height 56
   PainChance 128
   Mass 400
   Meleedamage 6
   REACTIONTIME 8
   MONSTER
   +NOGRAVITY
   +FLOAT
   +NOICEDEATH
   SeeSound "caco/sight"
   PainSound "caco/pain"
   DeathSound "caco/death"
   ActiveSound "caco/active"
   HitObituary "%o was mowed down by an enhanced cacodemon."
   Obituary "%o was made into a snack by the enhanced cacodemon."
   States
   {
   Spawn:
      ENCD A 10 A_Look
      Loop
   See:
      ENCD A 3 A_Chase
      Loop
   Missile:
      ENCD BC 5 A_FaceTarget
      ENCD B 0 bright A_CustomMissile("CacodemonBall", 24, 0, 0, 1)
      ENCD B 0 bright A_CustomMissile("CacodemonBall", 24, 0, -8, 1)
      ENCD B 5 bright A_CustomMissile("CacodemonBall", 24, 0, 8, 1)
      Goto See
   Pain:
      ENCD D 4
      ENCD D 4 A_Pain
      ENCD D 5
      Goto See
   Pain.Stunner:
       ENCD D 2 A_ChangeFlag("NoPain", 1)
       ENCD D 300 A_Pain
       ENCD D 0 A_ChangeFlag("NoPain", 0)
       goto See
      Pain.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        ENCD D 2 A_FaceTarget
        TNT1 A 0 A_Recoil(14)
        ENCD D 16 A_Pain
        Goto See 
      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
	TNT1 A 0 A_GiveToTarget("AwardItem", 10)
        TNT1 A 0 A_NoBlocking
        ENCD D 0 A_GenericFreezeDeath
        ENCD D 70 A_UnsetSolid
        ENCD D 0 A_IceGuyDie
        ENCD D 1 A_IceGuyDie
        TNT1 A -1
        stop
      Death.DukeExplosive:
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death
      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death
   Death.ZorchPistol:
   Death.Zorch:
	TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("AwardItem", 10)
        TNT1 A 0 A_GiveToTarget("KillCount", 1)	
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        ENCD D 4 A_SpawnItem("ZorchEffectBig")
        ENCD DDDDDDDDDDDD 4 Bright A_Fadeout(0.1)
	Stop
   Death:
      TNT1 A 0 A_GiveToTarget("KillCount", 1)
      TNT1 A 0 A_GiveToTarget("AwardItem", 10)
      ENCD E 8
      ENCD F 8 A_Scream
      ENCD GH 8
      ENCD I 8 A_NoBlocking
      ENCD J 3000 A_SetFloorClip
      ENCD JJJJJJJJJJJJ 2 A_FadeOut(0.1)
      Stop
   Raise:
      Stop
   }
}

