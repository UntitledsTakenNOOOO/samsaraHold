ACTOR Wicked 30133 
{ 
//$Category Stronghold_Monsters
spawnid 227
painchance "Stunner", 128
painchance "MightyBoot", 255
DamageFactor "CorvusCrossbow", 1.1
DamageFactor "DragonClaw", 1.5
DamageFactor "HexenIce", 1.25
DamageFactor "DukeIce", 1.25
Health 275 
Radius 25 
Height 88 
Mass 200 
Speed 10 
PainChance 112 
MONSTER 
+NOGRAVITY 
+FLOAT
+NOICEDEATH
Obituary "%o got blasted by a Wicked."
SeeSound "monster/wiksit" 
PainSound "monster/wikpai" 
DeathSound "monster/wikdth" 
ActiveSound "monster/wikact" 
States 
   { 
   Spawn: 
       WICK ABCD 8 A_Look 
       Loop 
   See:
       WICK AABBCCDD 4 A_Chase
       WICK A 0 A_Jump(64,1)
       Goto See
       WICK A 0
       Goto See2
   See2:
       WICK EEFFGGHH 2 A_Chase
       WICK E 0 A_Jump(32,1)
       Goto See2
       WICK E 0
       Goto See
   Missile:
       WICK I 1 A_PlaySound("monster/wikatk")
       WICK I 4 A_FaceTarget
       WICK J 5 BRIGHT A_FaceTarget  
       WICK K 8 BRIGHT A_CustomMissile("WickedShot",60,0,0)
       WICK L 5 
       Goto See       
   Pain: 
       WICK M 3 
       WICK M 3 A_Pain 
       WICK M 3 
       Goto See 
   Pain.Stunner:
       WICK M 2 A_ChangeFlag("NoPain", 1)
       WICK M 300 A_Pain
       WICK M 0 A_ChangeFlag("NoPain", 0)
       goto See
      Pain.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        WICK M 2 A_FaceTarget
        TNT1 A 0 A_Recoil(12)
        WICK M 16 A_Pain
        Goto See 
      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
	TNT1 A 0 A_GiveToTarget("AwardItem", 12)
        TNT1 A 0 A_NoBlocking
        WICK M 0 A_GenericFreezeDeath
        WICK M 70 A_UnsetSolid
        WICK M 0 A_IceGuyDie
        WICK M 1 A_IceGuyDie
        TNT1 A -1
        stop
      Death.DukeExplosive:
      Death.DukeDamage:
      Death.DukePistol:
        TNT1 A 0 A_Jump(218,"Death")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
        goto Death
      Death.MightyBoot:
        TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
        TNT1 A 0 A_Jump(64,"Death")
        TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
        goto Death
      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
	TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("AwardItem", 12)
        TNT1 A 0 A_GiveToTarget("KillCount", 1)	
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        WICK M 4 A_SpawnItem("ZorchEffectBig")
        WICK MMMMMMMMMMMM 4 Bright A_Fadeout(0.1)
	Stop       
   Death:
       TNT1 A 0 ACS_ExecuteAlways(SAMSARA_CLIENT_DECORATE,0,11)
       TNT1 A 0 A_GiveToTarget("KillCount", 1)
       TNT1 A 0 A_GiveToTarget("AwardItem", 12)
       WICK N 5 A_Scream
       WICK OP 5 
       WICK P 0 A_SpawnItemEX("WickedTorso",0,0,48,0,0,0,0,16)
       WICT A 5 A_NoBlocking
       WICT G 3000 A_SetFloorClip
       WICT GGGGGGGGGGGG 2 A_FadeOut(0.1)   
       Stop 
   Raise:
       Stop

	DeathBrutal:
         TNT1 A 0
                TNT1 AA 0 A_SpawnItemEx("RedMistTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
                TNT1 AAA 0 A_SpawnItemEx("BrutalTinyBone",random(-5,5),random(-5,5),random(12,40),random(-200,200)/100.00,random(-200,200)/100.00,random(300,800)/100.00,random(-180,180),160)
                TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTinier",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
                TNT1 AAA 0 A_SpawnItemEx("BrutalChunkTiny",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
                TNT1 A 0 A_SpawnItemEx("RedMistIntenseTrail",random(-10,10),random(-10,10),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,600)/100.00,random(-180,180),160)
                TNT1 AA 0 A_SpawnItemEx("Switchable_FlyingBlood",random(-10,10),random(-10,10),random(12,40),random(-350,350)/100.00,random(-350,350)/100.00,random(300,700)/100.00,random(-180,180),160)
                TNT1 A 0 A_SpawnItemEx("BrutalOnlineSound2",0,0,0,0,0,0,0,160) // Apparently A_PlaySound doesn't work, so I have to spawn the item. Weird!
        goto Death+1
                
         DeathHappyfun:
         TNT1 A 0
                TNT1 AAAAA 0 A_SpawnItemEx("RainbowSparkleSound",random(-10,10),random(-10,10),random(32,40),0,0,0,0,32)
                TNT1 AAAA 0 A_SpawnItemEx("WhiteSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
                TNT1 A 0 A_SpawnItemEx("RainbowSparkHuge",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
                TNT1 AAAAAA 0 A_SpawnItemEx("RainbowSpark",0,0,random(32,40),random(-60,60)/20.0,random(-60,60)/20.0,random(-40,40)/20.0,random(-180,180),32)
                TNT1 AAA 0 A_SpawnItemEx("RainbowSparkTiny",random(-5,5),random(-5,5),random(25,30),random(-80,80)/10.00,random(-80,80)/10.0,random(-3,3),random(-180,180),160)
        goto Death+1
   } 
}

ACTOR WickedShot
{
   Radius 8
   Height 8
   Speed 10
   Damage 5
   RENDERSTYLE ADD
   DamageType Fire
   ALPHA 0.67
   PROJECTILE
   +SEEKERMISSILE
   +FORCEXYBILLBOARD
   +THRUGHOST
   Seesound "weapons/firmfi"
   DeathSound "weapons/firex5"
   States
   {
   Spawn:
      WIBL ABCDABCDABCDABCDABCDABCDABCD 1 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
      WIBL ABCD 0 Bright A_SeekerMissile(90,90)
      loop
   Death:
      WIBL JKLMN 3 Bright
      stop
   }
}

ACTOR WickedFX
{   
   Radius 2
   Height 2
   +NOBLOCKMAP
   +NOGRAVITY
   +DONTSPLASH
   +FORCEXYBILLBOARD
   RENDERSTYLE ADD
   ALPHA 0.67
   States
   {
   Spawn:
      TNT1 A 3 Bright
      WIBL EFGHI 3 BRIGHT
      Stop 
   }
}

ACTOR WickedTorso
{
   Health 1000000000000
   ReactionTime 35
   Mass 1000000
   Radius 1
   Height 1
   MONSTER
   -COUNTKILL
   -ISMONSTER
   +NONSHOOTABLE
   +NORADIUSDMG  
   +DROPOFF
   +NOTELEPORT
   +NOTARGET
   +CORPSE
   States
   {
   Spawn:
      WICK Q 5 A_PlaySound("monster/tenpn1")
      WICK R 5 A_CheckFloor("Ground")
      WICK R 0 A_Countdown
      goto Spawn+1
   Ground:
      WICK A 0 A_Die
      Stop
   Death:
      WICK S 1 A_SetFloorClip
      WICK S 4 A_PlaySound("monster/tenpn2")
      WICK TUVW 5
      WICK W 3000 
      WICK WWWWWWWWWWWW 2 A_FadeOut(0.1)   
      Stop     
   }
}

ACTOR WickedTorsoRes
{   
   Radius 24
   Height 24
   +NOGRAVITY
   +NOBLOCKMAP
   +NOCLIP
   States
   {
   Spawn:
      WICK WVUTSRQ 5
      stop     
   }
}
//End of Wicked
