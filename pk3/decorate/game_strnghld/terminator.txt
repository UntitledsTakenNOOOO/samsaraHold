///////////////////////////////////////////////////////////////////
//Terminator
//created by: Vader
///////////////////////////////////////////////////////////////////


ACTOR Terminator 30666
{ 
//$Category Stronghold_Monsters
painchance "Stunner", 128
DamageFactor "CorvusCrossbow", 1.1
DamageFactor "DragonClaw", 1.5
DamageFactor "MarathonFusion", 3.0
DamageFactor "WMCNade", 0.75
DamageFactor "CorvusHellStaffTomed", 0.2
DamageFactor "Kamikaze", 0.25
damagefactor "ZorchKamikaze", 0.25
SpawnID 231
Health 7000 
Radius 24 
Height 64 
Mass 1500 
Speed 12 
PainChance 8
scale 1.2
MONSTER
Obituary "\c[U2]%o \c[U2] was liquidated by a Terminator."
+FLOORCLIP 
+BOSS
+MISSILEMORE
+MISSILEEVENMORE
+DONTHURTSPECIES
+NORADIUSDMG
+DONTMORPH
+NOICEDEATH
Missiletype Grenadeblast
Damagefactor "Redblast", 0.0
MeleeRange 192
SeeSound "monster/termsit" 
PainSound "monster/termpin" 
DeathSound "monster/termdth"
ActiveSound "Terminator/termact"
States 
   { 
   Spawn: 
       TERM AB 10 A_Look 
       Loop
      See:
        TNT1 A 0 A_JumpIfInventory("DukeBossSee",1,"See2")
        TNT1 A 0 A_GiveInventory("DukeBossSee",1)
        TNT1 A 0 A_GiveToTarget("DukeBossSightTaunt")
      See2: 
       TERM A 0 A_Playsound("Terminator/terstepA")
       TERM A 4 A_Chase
       TERM A 4 A_Chase
       TERM B 4 A_Chase
       TERM B 4 A_Chase
       TERM C 0 A_Playsound("Terminator/terstepA")
       TERM C 4 A_Chase 
       TERM C 4 A_Chase
       TERM D 4 A_Chase
       TERM D 4 A_Chase
       Loop 
   Pain:
       TERM L 3 	
       TERM L 3 A_Pain
       Goto See
   Pain.Stunner:
       TERM L 2 A_ChangeFlag("NoPain", 1)
       TERM L 300 A_Pain
       TERM L 0 A_ChangeFlag("NoPain", 0)
       goto See
   Melee:
       Goto Missile2+1
   Missile:
       TERM E 0 A_JumpIfCloser(512, "Missile2") 
       TERM E 0 A_Jump(170, "Missile2") 
       TERM E 5 A_FaceTarget
       TERM F 5 Bright A_CustomMissile("TerminatorBlaster",38,15,Random(Random(20,Random(30,40)),Random(-20,(Random(-30,-40))))/10.0)
       TERM F 0 A_Spidrefire
       Goto Missile+2
    Missile2:
       TERM G 0 A_Jump(128, "Missile3")
       TERM G 2 A_Facetarget
       TERM H 1 Bright A_CustomBulletAttack (24, 12, 6, 3, "Terminatorpuff")
       TERM H 1 Bright A_Playsound("Terminator/tershotB")
       TERM G 2 A_Facetarget
       TERM I 1 Bright A_CustomBulletAttack (24, 12, 6, 3, "Terminatorpuff")
       TERM I 1 Bright A_Playsound("Terminator/tershotB")
       TERM G 1 A_Spidrefire
       Goto Missile2+1
    Missile3:
       TERM J 0 A_Jump(128,"Missile5")
       TERM K 0 A_JumpIfCloser(192,"Missile4")
       TERM J 8 A_Facetarget
       TERM K 8 Bright A_CustomMissile("Redgrenade", 38, -15, 0, 0)
       Goto See
    Missile4:
       TERM J 6 A_Facetarget
       TERM K 6 Bright A_CustomMissile("Redstar", 38, -15, 0, Random(2,-8))
       TERM J 6 A_Facetarget
       TERM K 6 Bright A_CustomMissile("Redstar", 38, -15, 0, Random(8,-2))
    Missile5:
       TERM J 6 A_Facetarget
       TERM K 6 Bright A_CustomMissile("Redstar", 38, -15, 0, 0)
       Goto See
      Death.DukeIce:
        TNT1 A 0 A_PlaySound("duke/enemyfreeze")
        TNT1 A 0 A_GiveInventory("FreezethrowerWeakness")
        TNT1 A 0 A_Jump(218,"Ice")
        TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)

      Ice:
      Death.HexenIce:

      Death.Ice:
        TNT1 A 0 A_GiveToTarget("KillCount",1)
	TNT1 A 0 A_GiveToTarget("AwardItem", 85)
        TNT1 A 0 A_NoBlocking
        TERM L 0 A_GenericFreezeDeath
        TERM L 70 A_UnsetSolid
        TERM L 0 A_IceGuyDie
        TERM L 1 A_IceGuyDie
        TNT1 A -1
        stop
      Death.ZorchPistol:
      Death.Zorch:
      Death.ZorchKamikaze:
      Death.PhasingZorch:
      Death.ZorchSpectral:
	TNT1 A 0 A_Stop
        TNT1 A 0 A_GiveToTarget("AwardItem", 80)
        TNT1 A 0 A_GiveToTarget("KillCount", 1)	
        TNT1 A 0 A_ChangeFlag("DONTGIB",1)
        TNT1 A 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
        TERM L 4 A_SpawnItem("ZorchEffectHuge")
        TERM LLLLLLLLLLLL 4 Bright A_Fadeout(0.1)
	Stop
   Death.MarathonFusion:
       TNT1 A 0 A_GiveToTarget("KillCount", 1)
       TNT1 A 0 A_GiveToTarget("AwardItem", 100)
       TERM M 6 A_Scream
       TERM N 4 A_Explode(Random(200,250),160,0)
       TERM OPQ 4 Bright
       TERM R 0 Bright A_SpawnItemEx("TerminatorHead", 15, 0, 90, 10, 0, 0, -170, 128)
       TERM R 0 Bright A_SpawnItemEx("TerminatorShoulder", 60, 0, 75, 8, 0, 0, -70, 128)
       TERM R 4 Bright A_SpawnItemEx("TerminatorArm", 60, 0, 0, 3, 0, 0, -90, 128)
       TERM STUV 4 Bright
       TERM W 4 
       TERM X 6 A_Fall
       TERM Y 6
       TERM Z 1 A_Playsound("Terminator/terstepA")
       TERM Z 5 
       TERM [ 3000
       TERM "[[[[[[[[[[[[" 2 A_FadeOut(0.1) 
       Stop
      Death.DukeDamage:
      Death.MightyBoot:
      Death.DukeExplosive:
      Death.DukePistol:
        TNT1 A 0 A_GiveToTarget("DukeBossKillTaunt",1)
   Death:
       TNT1 A 0 A_GiveToTarget("KillCount", 1)
       TNT1 A 0 A_GiveToTarget("AwardItem", 75)
       TERM M 6 A_Scream
       TERM N 4 
       TERM OPQ 4 Bright
       TERM R 0 Bright A_SpawnItemEx("TerminatorHead", 15, 0, 90, 10, 0, 0, -170, 128)
       TERM R 0 Bright A_SpawnItemEx("TerminatorShoulder", 60, 0, 75, 8, 0, 0, -70, 128)
       TERM R 4 Bright A_SpawnItemEx("TerminatorArm", 60, 0, 0, 3, 0, 0, -90, 128)
       TERM STUV 4 Bright
       TERM W 4 
       TERM X 6 A_Fall
       TERM Y 6
       TERM Z 1 A_Playsound("Terminator/terstepA")
       TERM Z 5 
       TERM [ 3000
       TERM "[[[[[[[[[[[[" 2 A_FadeOut(0.1) 
       Stop
   } 
}


ACTOR TerminatorBlaster
{
   Radius 8
   Height 12
   Speed 35 //Too Slow?
   FastSpeed 45 //More like NOT SLOW ENOUGH.
   Damage 10
   DamageType "Normal"
   SpawnID 251
   PROJECTILE
   RENDERSTYLE ADD
   alpha 0.85
   SeeSound "monster/brufir"
   DeathSound "weapons/hellex"
   +FORCERADIUSDMG //Not today, Parias.
   States
   {
   Spawn: 
      BLST A 1 Bright 
      loop
   Death:
      BLST B 6 Bright A_Explode(16,128,1)
      BLST CDEFGHIJKL 3 Bright 
      stop
   }
}

actor Redstar
{
    Radius 10
    Height 20
    Speed 30 //Ohhhhhhhhhhhhhhhh...
    FastSpeed 40 //I've having way too much fun making this a hard enemy, aren't I...
    Damage 4
    DamageType "Normal"
    Scale 0.6
    +SEEKERMISSILE
    +FORCERADIUSDMG
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.8
    SeeSound "Terminator/tershotA"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        STAX A 0 Bright A_SeekerMissile (15,45) //Welp.
	STAX AAAA 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
        STAX B 0 Bright A_SeekerMissile (15,45) //Good luck dodging.
	STAX BBBB 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
        STAX C 0 Bright A_SeekerMissile (15,45) //I've tested. You're gonna need it.
	STAX CCCC 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
        STAX D 0 Bright A_SeekerMissile (15,45) //Grenade Trails make it harder to judge too.
	STAX DDDD 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
        Loop
    Death:
        STAX E 3 Bright A_Explode(20,192,1)
        STAX FGHIJKLMNO 3 Bright
        Stop
    }
}

actor Redgrenade //More like Red Missile now. Heh heh.
{
    Radius 6
    Height 12
    Speed 30
    FastSpeed 45
    Damage 4
    DamageType "Redblast"
    OBITUARY "\c[U2]%o \c[U2]was torn apart by a blood missile."
    PROJECTILE
    //Heh heh. Players won't know what hit them. Hopefully neither will Popsoap.
    //+DoomBounce
    //-NoGravity
    RENDERSTYLE ADD
    ALPHA 0.8
    DeathSound "imp/shotx"
    States
    {
    Spawn:
    	TGND A 0 Bright A_Playsound("Terminator/tershotA")
    	TGND AAAABBBBCCCC 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
    	Goto Spawn+1
    Death:
    	STAR E 3 Bright
    	STAR E 0 Bright A_custommissile("grenadeblast",0,0,0,2,0)
    	STAR E 0 Bright A_custommissile("grenadeblast",0,0,20,2,0)
    	STAR E 0 Bright A_custommissile("grenadeblast",0,0,40,2,0)
    	STAR E 0 Bright A_custommissile("grenadeblast",0,0,60,2,0)
    	STAR E 0 Bright A_custommissile("grenadeblast",0,0,80,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,100,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,120,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,140,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,160,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,180,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,200,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,220,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,240,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,260,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,280,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,300,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,320,2,0)
        STAR E 0 Bright A_custommissile("grenadeblast",0,0,340,2,0)
        STAR FGHIJKLMNO 3 Bright
        Stop
    }
}

ACTOR Grenadetrail
{       
   	PROJECTILE
   	+NOCLIP
    	Renderstyle ADD
	Alpha 0.6
	States
	{   
	Spawn:
	    TGTR ABCDEFGHI 4 Bright
	    Stop
	}
}  


actor Grenadeblast
{
    Radius 6
    Height 12
    Speed 6
    Damage 3
    OBITUARY "\c[U2]%o \c[U2]was torn apart by a blood missile."
    DamageType "Redblast"
    PROJECTILE
    RENDERSTYLE ADD
    +Ripper
    +NoDamageThrust
    ALPHA 0.8
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
    	TGBL ABCDEFGHIJK 2 Bright
	Stop
    }
}

ACTOR Terminatorpuff : Bulletpuff
{          
    	Renderstyle ADD
	Alpha 0.75
	scale 0.75
	States
	{   
	Spawn:
	Melee:
	    TPUF A 2 Bright
	    TPUF A 0 A_Jump(128,10)
	    TPUF BCDEFGH 2 Bright
	    Stop
	    TPUF IJKLMNO 2 Bright 
	    Stop
	}
}  

ACTOR TerminatorArm
{
	Radius 12
	Height 15
	Speed 3
	Mass 1000
	scale 1.2
	+DOOMBOUNCE
      States
	{
	Spawn:
         TARM A 5
         TARM B 5 A_Fall 
         TARM CDE 5
         TARM F 3000
         TARM FFFFFFFFFFFF 2 A_FadeOut(0.1)
         Stop
	}
}

actor TerminatorHead
{
    Radius 6
    Height 8
    Speed 10
    Mass 1000
    +DOOMBOUNCE
    +DROPOFF
    +Missile
    scale 1.2
    
    States
    {
    Spawn:
    	THAD DEFGHABC 2
        loop
    Death:
    	THAD I 3000
	THAD IIIIIIIIIIII 2 A_FadeOut(0.1)
        loop
    }
}

actor TerminatorShoulder
{
    Radius 8
    Height 8
    Speed 8
    Mass 1000
    +DOOMBOUNCE
    +DROPOFF
    +Missile
    scale 1.2
    
    States
    {
    Spawn:
    	TSHO ABCDEFGH 2
        loop
    Death:
    	TSHO I 3000
	TSHO IIIIIIIIIIII 2 A_FadeOut(0.1)
        loop
    }
}
